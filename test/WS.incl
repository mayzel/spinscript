
define delay mixingTime

"p11=1m"
"p12=2m"
"spnam11='Sinc1.1000'"
"spw11=plw1*pow(p1/(p11*integfac11),2)"
"spoal11=0.5"

"p13=4m"
"spnam12='Sinc1.1000'"
"spw12=plw1*pow(p1/(p13*integfac12),2)"
"spoal12=0.5"

subroutine es(phase pha, phase phb, gradient grada, gradient gradb)
{
  p16:grada d16
  (p12:sp11 pha:r):f1
  (p2 pha+180 pl1 ):f1
  p16:grada d16
  "TAU=de+p1*2/3.1416"
  TAU
  p16:gradb d16
  (p12:sp11 phb:r):f1
  (p2 phb+180 pl1 ):f1
  p16:gradb d16
}

subroutine wg(phase pha, gradient grad)
{
  p16:grad d16
  de
  "TAU=p1*2/PI"
  (p11:sp11 pha:r):f1
  20u
  (p2 pha+180 pl1):f1
  20u
  (p11:sp11 pha:r):f1
  TAU
  p16:grad d16
}

subroutine fb(phase pha)
{
#ifdef FB
  (p13:sp12 pha):f1
#endif
}

subroutine recoveryDelay()
{
  d12 BLKGRAD
#ifdef PRESAT
  (d1 cw pl9):f1 
  d12 do:f1
#else
  d1
#endif
  d12 UNBLKGRAD
}

subroutine ws()
{
#ifdef ES  
  subr es(ph2, ph3, gp1, gp2)
#elif defined WG
  subr wg(ph2, gp1)
#endif
}

subroutine setDecPower()
{
#ifdef IGF2
  4u pl12:f2
#endif
#ifdef IGF3
  4u pl16:f3
#endif
}

subroutine acqu()
{
#if (defined IGF2) && (defined IGF3)
  go=2 ph31 cpd2:f2 cpd3:f3 finally do:f2 do:f3
#elif defined IGF2
  go=2 ph31 cpd2:f2 finally do:f2
#elif defined IGF3 
  go=2 ph31 cpd3:f3 finally do:f3
#else
  go=2 ph31
#endif
}